plugins {
	id "net.minecraftforge.gradle.forge" version "41dfce0a70"
	id "com.github.johnrengelman.shadow" version "6.1.0"
	id "org.spongepowered.mixin" version "0.6-SNAPSHOT"
}

version="2.0.6"
group = "com.golem.golemmod"
archivesBaseName = "golemmod"

sourceCompatibility = targetCompatibility = 1.8
compileJava.options.encoding = 'UTF-8'



minecraft {
	version = "1.8.9-11.15.1.2318-1.8.9"
	runDir = "run"
	mappings = "stable_22"
	makeObfSourceJar = false
	clientRunArgs += ["--tweakClass gg.essential.loader.stage0.EssentialSetupTweaker", "--mixin mixins.golemmod.json"]
}

repositories {
	mavenCentral()
	maven { url "https://jitpack.io" }
	maven { url "https://repo.spongepowered.org/repository/maven-public/" }
	maven { url "https://repo.sk1er.club/repository/maven-public" }
	maven { url "https://repo.sk1er.club/repository/maven-releases/" }
}

configurations {
	packageLib
	configurations.implementation.extendsFrom(configurations.packageLib)
}


dependencies {
	// Backend
	packageLib "javax.websocket:javax.websocket-api:1.1"
	packageLib "org.java-websocket:Java-WebSocket:1.5.1"
	// Mixins
	annotationProcessor("org.spongepowered:mixin:0.7.11-SNAPSHOT")
	implementation("org.spongepowered:mixin:0.7.11-SNAPSHOT")
	// Essential gui
	packageLib("gg.essential:loader-launchwrapper:1.1.1")
	implementation("gg.essential:essential-1.8.9-forge:1498")
	// Discord
	implementation 'org.javacord:javacord:3.8.0'

	// Inc Tests
	testImplementation 'junit:junit:4.13.2'
}

mixin {
	disableRefMapWarning = true
	defaultObfuscationEnv searge
	add sourceSets.main, "mixins.golemmod.refmap.json"
}

jar {

	duplicatesStrategy = DuplicatesStrategy.EXCLUDE

	manifest.attributes(
			"FMLCorePluginContainsFMLMod": true,
			"FMLCorePlugin": "com.golem.golemmod.injection.FMLPlugin",
			"ForceLoadAsMod": true,
			"MixinConfigs": "mixins.golemmod.json",
			"ModSide": "CLIENT",
			"TweakClass": "gg.essential.loader.stage0.EssentialSetupTweaker",
			"TweakOrder": "0"
	)

	enabled = false
}

shadowJar {
	archiveFileName = jar.archiveFileName
	duplicatesStrategy = DuplicatesStrategy.EXCLUDE
	configurations = [project.configurations.packageLib]
}

reobf {
	shadowJar {
		classpath = sourceSets.main.compileClasspath
	}
}

processResources {
	inputs.property "version", project.version
	inputs.property "mcversion", project.minecraft.version
	from(sourceSets.main.resources.srcDirs) {
		include 'mcmod.info'
		expand 'version':project.version, 'mcversion':project.minecraft.version
	}
	from(sourceSets.main.resources.srcDirs) {
		exclude 'mcmod.info'
	}
}

sourceSets {
	main { output.resourcesDir = java.outputDir }
}

tasks.reobfJar.dependsOn(shadowJar)